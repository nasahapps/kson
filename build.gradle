buildscript {
    ext.kotlin_version = "1.3.11"

    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$kotlin_version"
    }
}

repositories {
    jcenter()
}

apply plugin: "kotlin-multiplatform"
apply plugin: "maven-publish"

group = "com.nasahapps.kson"
version = "1.0.2"

kotlin {
    targets {
        fromPreset(presets.jvm, "jvm")
        fromPreset(presets.macosX64, 'mac')
        fromPreset(presets.iosArm64, 'iOS')
        fromPreset(presets.iosX64, 'iOSSimulator')
    }
    sourceSets {
        commonMain.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
        }
        commonTest.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
            implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"
        }
        jvmMain.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
            implementation "org.json:json:20180813"
        }
        jvmTest.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
            implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        }
        nativeMain.dependencies {

        }
        nativeTest.dependencies {

        }
        configure([macMain, iOSMain, iOSSimulatorMain]) {
            dependsOn nativeMain
        }
        configure([macTest, iOSTest, iOSSimulatorTest]) {
            dependsOn nativeTest
        }
    }
}